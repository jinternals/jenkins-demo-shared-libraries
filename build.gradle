plugins {
    id 'groovy'
}

group 'com.jinternals'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile "com.lesfurets:jenkins-pipeline-unit:1.1"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

test {
    // delete old test reports
    dependsOn cleanTest

    // don't stop if tests fail
    ignoreFailures = true

    // minimize logging
    testLogging.maxGranularity = 0

    // show stdout from tests
    onOutput { dest, event -> print event.message }

    // show test results
    def results = []
    afterTest { desc, result -> println "${desc.className.split("\\.")[-1]}: " +
            "${desc.name}: ${result.resultType}" }
    afterSuite { desc, result -> if (desc.className) { results << result } }

    // show summary
    doLast {
        println "Tests: ${results.sum { it.testCount }}" +
                ", Failures: ${results.sum { it.failedTestCount }}" +
                ", Errors: ${results.sum { it.exceptions.size() }}" +
                ", Skipped: ${results.sum { it.skippedTestCount }}"
    }
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src','vars']
        }
        resources {
            srcDirs = ['resources']
        }
    }
    test {
        groovy {
            srcDirs = ['test/groovy']
        }
        resources {
            srcDirs = ['test/resources']
        }
    }
}
